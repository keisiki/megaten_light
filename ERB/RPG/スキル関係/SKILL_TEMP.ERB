
;=========================================
;1체공격스킬기본
;=========================================
@ATTACK_SINGLE,ARG,ARG:1,ARG:2,ARG:3
#LOCALSIZE 13
#DIM num
;ARG:0 사용者
;ARG:1 대상
;ARG:2 사용스킬
;ARG:3 真の場合最後にWAITで待たない
;コストを支払う
;CALL PAY_COST,ARG,ARG:2
VARSET num,0



;공격횟수算出
CALLFORM SKILL_MAXATTACKNUMBER_{ARG:2}, ARG
LOCAL = RESULT + 1
CALLFORM SKILL_MINATTACKNUMBER_{ARG:2}, ARG
LOCAL -= RESULT
LOCAL:1 = RESULT + RAND:LOCAL
;LOCAL:1 공격횟수

FOR LOCAL:10, 0, LOCAL:1
	FLAG:공격횟수 = LOCAL:10
	CALLFORM SKILL_HITRATE_{ARG:2}, ARG
	LOCAL:2 = RESULT
	;LOCAL:2 스킬명중률
	CALLFORM SKILL_POWER_{ARG:2}, ARG
	LOCAL:3 = RESULT
	;LOCAL:3 스킬威힘
	CALLFORM SKILL_TYPE_{ARG:2}, ARG
	LOCAL:4 = RESULT
	;LOCAL:4 스킬相性
	CALLFORM SKILL_DAMAGETYPE_{ARG:2}
	LOCAL:5 = RESULT
	;LOCAL:5 与えるダメージの物理/魔法
	
	;추가효과を参照
	LOCAL:6 = 0
	

	TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ARG:2}, ARG
		LOCAL:6 = RESULT
		CALLFORM SKILL_ADDTIONAL_TYPE_{ARG:2}, ARG
		LOCAL:7 = RESULT
		CALLFORM SKILL_ADDTIONAL_BASE_RATE_{ARG:2}, ARG
		LOCAL:8 = RESULT
		CALLFORM SKILL_ADDTIONAL_MAX_RATE_{ARG:2}, ARG
		LOCAL:9 = RESULT
	CATCH
	ENDCATCH
	LOCAL:11 = 0
	;物理스킬なら추가威힘参照
	;CALLFORM SKILL_COSTTYPE_{ARG:2}, ARG
	;IF RESULT == 2
	;	CALLFORM SKILL_COST_{ARG:2}, ARG
	;	LOCAL:11 = MAXBASE:ARG:ＨＰ * RESULT / 100
	;	SIF CFLAG:ARG:ボスフラグ
	;		LOCAL:11 /= 10
	;ENDIF

	;스킬の흡수率
	TRYCCALLFORM SKILL_HP_ABSORBER_{ARG:2},ARG
		LOCAL:11 = RESULT
	CATCH
		LOCAL:11 = 0
	ENDCATCH
	SIF CFLAG:ARG:ステート == GET_STATE_NUM("CURSE")
		LOCAL:11 -= 50
		
	TRYCCALLFORM SKILL_CRITICAL_RATE_{ARG:2}
		LOCAL:12 = RESULT
	CATCH
		LOCAL:12 = 5
	ENDCATCH
	PRINTFORM 　　TARGET:[{CPOS(ARG:1),2}] %CALLNAME:(ARG:1),20,LEFT%　>>>>>>　

	CALLFORM JUDG_HIT_{LOCAL:5},ARG,ARG:1,LOCAL:2
	IF RESULT == 1
		CALL ATTACK_HIT,ARG,ARG:1,LOCAL:3,LOCAL:4,LOCAL:5,LOCAL:6,LOCAL:7,LOCAL:8,LOCAL:9,LOCAL:11,LOCAL:12
		
		SIF !ARG:3
			WAIT
		;구상用に数値確保
		;CALL BATTLE_EVENT_ATTACK, ARG, ARG:1, 0, 3
	ELSE
		;外した
		PRINTL MISS
		SIF !HAVE_SKILL(ARG, [[스킬:엑스트라원]] ) && !CFLAG:ARG:PTフラグ && BATTLE_SETTING_IS_1MORE()
			CFLAG:ARG:１moreフラグ -= 1
		SIF TFLAG:스웨이리액트 && NO:(ARG:1) == [[キャラ:로어=아=루아]]
			num = num + 1
		SIF !ARG:3
			WAIT
		;구상用に数値確保
		CALL BATTLE_EVENT_ATTACK, ARG, ARG:1, 0, 2
	ENDIF
	CALL RECEIVE_SET,ARG:1,LOCAL:4,LOCAL:5
	FLAG:공격횟수 = 0
	SIF GET_STATE(CFLAG:(ARG:1):ステート) == "DYING"
		BREAK
NEXT
SIF num > 0
	CALL SPECIAL_ACTION_3537,CPOS(ARG:1),CPOS(ARG)


;=========================================
;전체・列공격스킬기본
;=========================================
@ATTACK_FIELD, ARG, ARG:1, ARG:2, ARG:3
#LOCALSIZE 16
#DIM num
#DIM num1
;ARG:0 사용者
;ARG:1 대상
;ARG:2 사용스킬
;ARG:3 真の場合最後にWAITで待たない
;コストを支払う
;CALL PAY_COST,ARG,ARG:2
VARSET num,0
VARSET num1,0

;공격する範囲を参照
IF ARG:1 == 17
	LOCAL:7 = 1
	LOCAL:8 = 4
ELSEIF ARG:1 == 18
	LOCAL:7 = 4
	LOCAL:8 = 7
ELSEIF ARG:1 == 19
	LOCAL:7 = 1
	LOCAL:8 = 7
ELSEIF ARG:1 == 20
	LOCAL:7 = 7
	LOCAL:8 = 12
ELSEIF ARG:1 == 21
	LOCAL:7 = 12
	LOCAL:8 = 17
ELSEIF ARG:1 == 22
	LOCAL:7 = 7
	LOCAL:8 = 17
ELSEIF ARG:1 == 23
	LOCAL:7 = 1
	LOCAL:8 = 17
ENDIF


;공격횟수算出
CALLFORM SKILL_MAXATTACKNUMBER_{ARG:2}, ARG
LOCAL = RESULT + 1
CALLFORM SKILL_MINATTACKNUMBER_{ARG:2}, ARG
LOCAL -= RESULT
LOCAL:1 = RESULT + RAND:LOCAL
;LOCAL:1 공격횟수




FOR LOCAL:9,LOCAL:7,LOCAL:8
	FOR LOCAL:6,0,LOCAL:1
		FLAG:공격횟수 = LOCAL:6
		CALLFORM SKILL_HITRATE_{ARG:2}, ARG
		LOCAL:2 = RESULT
		;LOCAL:2 스킬명중률
		CALLFORM SKILL_POWER_{ARG:2}, ARG
		LOCAL:3 = RESULT
		;LOCAL:3 스킬威힘
		CALLFORM SKILL_TYPE_{ARG:2}, ARG
		LOCAL:4 = RESULT
		;LOCAL:4 스킬相性
		CALLFORM SKILL_DAMAGETYPE_{ARG:2}
		LOCAL:5 = RESULT
		;LOCAL:5 与えるダメージの物理/魔法
		
		LOCAL:10 = 0
		;추가효과を参照
		TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ARG:2}, ARG
			LOCAL:10 = RESULT
			CALLFORM SKILL_ADDTIONAL_TYPE_{ARG:2}, ARG
			LOCAL:11 = RESULT
			CALLFORM SKILL_ADDTIONAL_BASE_RATE_{ARG:2}, ARG
			LOCAL:12 = RESULT
			CALLFORM SKILL_ADDTIONAL_MAX_RATE_{ARG:2}, ARG
			LOCAL:13 = RESULT
		CATCH
		ENDCATCH
		
		LOCAL:14 = 0
		;ＨＰ소비物理스킬なら추가威힘参照
		;CALLFORM SKILL_COSTTYPE_{ARG:2}, ARG
		;IF RESULT == 2
		;	CALLFORM SKILL_COST_{ARG:2}, ARG
		;	LOCAL:14 = MAXBASE:ARG:ＨＰ * RESULT / 100
		;	SIF CFLAG:ARG:ボスフラグ
		;		LOCAL:14 /= 10
		;ENDIF
		
		;스킬の흡수率
		TRYCCALLFORM SKILL_HP_ABSORBER_{ARG:2},ARG
			LOCAL:14 = RESULT
		CATCH
			LOCAL:14 = 0
		ENDCATCH
		SIF CFLAG:ARG:ステート == GET_STATE_NUM("CURSE")
			LOCAL:14 -= 50
		
		TRYCCALLFORM SKILL_CRITICAL_RATE_{ARG:2}
			LOCAL:15 = RESULT
		CATCH
			LOCAL:15 = 5
		ENDCATCH
		LOCALS = 포지션{LOCAL:9}
		SIF FLAG:LOCALS < 0
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):ステート) == "DYING"
			CONTINUE
		PRINTFORM 　　TARGET:[{CPOS(FLAG:LOCALS),2}] %CALLNAME:(FLAG:LOCALS),20,LEFT%　>>>>>>　
		CALLFORM JUDG_HIT_{LOCAL:5}, ARG, FLAG:LOCALS, LOCAL:2
		IF RESULT == 1
			;명중した
			CALL ATTACK_HIT,ARG,FLAG:LOCALS,LOCAL:3,LOCAL:4,LOCAL:5,LOCAL:10,LOCAL:11,LOCAL:12,LOCAL:13,LOCAL:14,LOCAL:15
		
			SIF !ARG:3
				WAIT
			;구상用に数値確保
			;CALL BATTLE_EVENT_ATTACK, ARG, ARG:1, 0, 3
		ELSE
			;外した
			PRINTL MISS
			SIF !HAVE_SKILL(ARG, [[스킬:엑스트라원]] ) && !CFLAG:ARG:PTフラグ && BATTLE_SETTING_IS_1MORE()
				CFLAG:ARG:１moreフラグ -= 1
			SIF TFLAG:스웨이리액트 && NO:(POS(LOCAL:9)) == [[キャラ:로어=아=루아]]
				num = num + 1
				num1 = FLAG:LOCALS
			SIF !ARG:3
				WAIT
			;구상用に数値確保
			CALL BATTLE_EVENT_ATTACK, ARG, FLAG:LOCALS, 0, 2
		ENDIF
	FLAG:공격횟수 = 0
	NEXT
	CALL RECEIVE_SET,FLAG:LOCALS,LOCAL:4,LOCAL:5
NEXT
SIF num > 0
	CALL SPECIAL_ACTION_3537,CPOS(num1),CPOS(ARG)

;=========================================
;랜덤공격스킬기본
;ARG:0 사용者
;ARG:1 대상
;ARG:2 사용스킬
;ARG:3 真の場合最後にWAITで待たない
;=========================================
@ATTACK_RAND, ARG, ARG:1, ARG:2, ARG:3
#LOCALSIZE 17
#DIM 대상リスト , 16
#DIM 공격済み횟수 , 17
#DIM 최대1체공격횟수 , 17
#DIM 대상人数
#DIM ループ , 2
#DIM num
#DIM num1
VARSET 공격済み횟수 , 0
VARSET num,0
VARSET num1,0

;コストを支払う
;CALL PAY_COST,ARG,ARG:2

;공격する範囲を参照
IF ARG:1 == 17
	LOCAL:7 = 1
	LOCAL:8 = 4
ELSEIF ARG:1 == 18
	LOCAL:7 = 4
	LOCAL:8 = 7
ELSEIF ARG:1 == 19
	LOCAL:7 = 1
	LOCAL:8 = 7
ELSEIF ARG:1 == 20
	LOCAL:7 = 7
	LOCAL:8 = 12
ELSEIF ARG:1 == 21
	LOCAL:7 = 12
	LOCAL:8 = 17
ELSEIF ARG:1 == 22
	LOCAL:7 = 7
	LOCAL:8 = 17
ENDIF
;各々の최대1체공격횟수を설정
VARSET 최대1체공격횟수, -1
FOR ループ, LOCAL:7, LOCAL:8
	SIF POS(ループ) == -1
		CONTINUE
	SIF GET_STATE(CFLAG:POS(ループ):ステート) == "DYING"
		CONTINUE
	TRYCCALLFORM SKILL_MAXATK_PER_{ARG:2},ARG
		최대1체공격횟수:ループ = RESULT
		;스킬に최저횟수の설정がある場合、최대횟수の調整を行う
		TRYCCALLFORM SKILL_MINATK_PER_{ARG:2},ARG
			최대1체공격횟수:ループ = RAND(RESULT, 최대1체공격횟수:ループ+1)
		CATCH
		ENDCATCH
	CATCH
		CALLFORM SKILL_MAXATTACKNUMBER_{ARG:2},ARG
		최대1체공격횟수:ループ = RESULT
	ENDCATCH
NEXT
;최대値が-1なら、공격が当たる조건を満たしたキャラが0なので関数を抜ける
SIF MAXARRAY(최대1체공격횟수) == -1
	RETURN 0
;최대値が0なら、대상全員が0回になってしまったので1回を誰かに与える
WHILE !MAXARRAY(최대1체공격횟수)
	ループ = RAND(LOCAL:7, LOCAL:8)
	SIF POS(ループ) == -1
		CONTINUE
	SIF GET_STATE(CFLAG:POS(ループ):ステート) == "DYING"
		CONTINUE
	최대1체공격횟수:ループ = 1
WEND

;공격횟수算出
CALLFORM SKILL_MAXATTACKNUMBER_{ARG:2},ARG
LOCAL = RESULT + 1
CALLFORM SKILL_MINATTACKNUMBER_{ARG:2},ARG
LOCAL -= RESULT
LOCAL:1 = RESULT + RAND:LOCAL
;LOCAL:1 공격횟수


LOCAL:15 = 0
;ＨＰ소비物理스킬なら추가威힘参照
;CALLFORM SKILL_COSTTYPE_{ARG:2}, ARG
;IF RESULT == 2
;	CALLFORM SKILL_COST_{ARG:2}, ARG
;	LOCAL:15 = MAXBASE:ARG:ＨＰ * RESULT / 100
;	SIF CFLAG:ARG:ボスフラグ
;		LOCAL:15 /= 10
;ENDIF

		
;스킬の흡수率
TRYCCALLFORM SKILL_HP_ABSORBER_{ARG:2},ARG
	LOCAL:15 = RESULT
CATCH
	LOCAL:15 = 0
ENDCATCH
SIF CFLAG:ARG:ステート == GET_STATE_NUM("CURSE")
	LOCAL:15 -= 50
		
TRYCCALLFORM SKILL_CRITICAL_RATE_{ARG:2}
	LOCAL:16 = RESULT
CATCH
	LOCAL:16 = 5
ENDCATCH

LOCAL = 0
$대상リスト作成
VARSET 대상リスト , 0
대상人数 = 0
FOR ループ , LOCAL:7 , LOCAL:8
	SIF POS(ループ) == -1
		CONTINUE
	SIF GET_STATE(CFLAG:POS(ループ):ステート) == "DYING"
		CONTINUE
	SIF 공격済み횟수:ループ >= 최대1체공격횟수:ループ
		CONTINUE
	대상リスト:대상人数 = ループ
	대상人数 ++
NEXT
SIF 대상人数 == 0
	RETURN 0
SIF LOCAL
	GOTO IN_FOR

FOR LOCAL,0,LOCAL:1
	
	CALLFORM SKILL_HITRATE_{ARG:2}, ARG
	LOCAL:2 = RESULT
	;LOCAL:2 스킬명중률
	CALLFORM SKILL_POWER_{ARG:2}, ARG
	LOCAL:3 = RESULT
	;LOCAL:3 스킬威힘
	CALLFORM SKILL_TYPE_{ARG:2}, ARG
	LOCAL:4 = RESULT
	;LOCAL:4 스킬相性
	CALLFORM SKILL_DAMAGETYPE_{ARG:2}
	LOCAL:5 = RESULT
	;LOCAL:5 与えるダメージの物理/魔法

	LOCAL:10 = 0
	;추가효과を参照
	TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ARG:2}, ARG
		LOCAL:10 = RESULT
		CALLFORM SKILL_ADDTIONAL_TYPE_{ARG:2}, ARG
		LOCAL:11 = RESULT
		CALLFORM SKILL_ADDTIONAL_BASE_RATE_{ARG:2}, ARG
		LOCAL:12 = RESULT
		CALLFORM SKILL_ADDTIONAL_MAX_RATE_{ARG:2}, ARG
		LOCAL:13 = RESULT
	CATCH
	ENDCATCH

	$IN_FOR
	LOCALS = 포지션{대상リスト:(RAND:대상人数)}
	SIF FLAG:LOCALS < 0
		GOTO 대상リスト作成
	SIF GET_STATE(CFLAG:(FLAG:LOCALS):ステート) == "DYING"
		GOTO 대상リスト作成
	SIF 공격済み횟수:(CPOS(FLAG:LOCALS)) >= 최대1체공격횟수:CPOS(FLAG:LOCALS)
		GOTO 대상リスト作成
	공격済み횟수:(CPOS(FLAG:LOCALS)) ++
	PRINTFORM 　　TARGET:[{CPOS(FLAG:LOCALS),2}] %CALLNAME:(FLAG:LOCALS),20,LEFT%　>>>>>>　
	CALLFORM JUDG_HIT_{LOCAL:5},ARG,FLAG:LOCALS,LOCAL:2
	IF RESULT == 1
			;명중した
			CALL ATTACK_HIT,ARG,FLAG:LOCALS,LOCAL:3,LOCAL:4,LOCAL:5,LOCAL:10,LOCAL:11,LOCAL:12,LOCAL:13,LOCAL:15,LOCAL:16
	
			SIF !ARG:3
				WAIT
			;구상用に数値確保
			;CALL BATTLE_EVENT_ATTACK, ARG, ARG:1, 0, 3
	ELSE
		;外した
		PRINTL MISS
		SIF !HAVE_SKILL(ARG, [[스킬:엑스트라원]] ) && !CFLAG:ARG:PTフラグ && BATTLE_SETTING_IS_1MORE()
			CFLAG:ARG:１moreフラグ -= 1
		SIF TFLAG:스웨이리액트 && NO:(FLAG:LOCALS) == [[キャラ:로어=아=루아]]
			num = num + 1
			num1 = FLAG:LOCALS
		SIF !ARG:3
			WAIT
		;구상用に数値確保
		CALL BATTLE_EVENT_ATTACK, ARG, FLAG:LOCALS, 0, 2
	ENDIF
	CALL RECEIVE_SET,FLAG:LOCALS,LOCAL:4,LOCAL:5
	CALL ACTIONABLE_CHARA,ARG
	SIF RESULT == 0
		RETURN 0
NEXT
SIF num > 0
	CALL SPECIAL_ACTION_3537,CPOS(num1),CPOS(ARG)

;=========================================
;拡散공격스킬기본
;ARG:0 사용者
;ARG:1 대상
;ARG:2 사용스킬
;ARG:3 真の場合最後にWAITで待たない
;=========================================
@ATTACK_SPREAD,ARG,ARG:1,ARG:2
#DIM num
#DIM num1
;コストを支払う
;CALL PAY_COST,ARG,ARG:2
VARSET num,0
VARSET num1,0

;공격する範囲を参照
IF ARG:1 < 7
	LOCAL:7 = 1
	LOCAL:8 = 7
ELSE
	LOCAL:7 = 7
	LOCAL:8 = 17
ENDIF


;공격횟수算出
CALLFORM SKILL_MAXATTACKNUMBER_{ARG:2},ARG
LOCAL = RESULT + 1
CALLFORM SKILL_MINATTACKNUMBER_{ARG:2},ARG
LOCAL -= RESULT
LOCAL:1 = RESULT + RAND:LOCAL
;LOCAL:1 공격횟수


LOCAL:14 = 0
;ＨＰ소비物理스킬なら추가威힘参照
;CALLFORM SKILL_COSTTYPE_{ARG:2}, ARG
;IF RESULT == 2
;	CALLFORM SKILL_COST_{ARG:2}, ARG
;	LOCAL:14 = MAXBASE:ARG:ＨＰ * RESULT / 100
;	SIF CFLAG:ARG:ボスフラグ
;		LOCAL:14 /= 10
;ENDIF
		
;스킬の흡수率
TRYCCALLFORM SKILL_HP_ABSORBER_{ARG:2},ARG
	LOCAL:14 = RESULT
CATCH
	LOCAL:14 = 0
ENDCATCH
SIF CFLAG:ARG:ステート == GET_STATE_NUM("CURSE")
	LOCAL:14 -= 50
		
TRYCCALLFORM SKILL_CRITICAL_RATE_{ARG:2}
	LOCAL:15 = RESULT
CATCH
	LOCAL:15 = 5
ENDCATCH

CALLFORM SKILL_SPREAD_{ARG:2},ARG
LOCAL:16 = RESULT+1
FOR LOCAL:17,0,LOCAL:16

	CALLFORM SKILL_HITRATE_{ARG:2}, ARG
	LOCAL:2 = RESULT
	;LOCAL:2 스킬명중률
	CALLFORM SKILL_POWER_{ARG:2}, ARG
	LOCAL:3 = RESULT
	;LOCAL:3 스킬威힘
	CALLFORM SKILL_TYPE_{ARG:2}, ARG
	LOCAL:4 = RESULT
	;LOCAL:4 스킬相性
	CALLFORM SKILL_DAMAGETYPE_{ARG:2}
	LOCAL:5 = RESULT
	;LOCAL:5 与えるダメージの物理/魔法
	
	LOCAL:10 = 0
	;추가효과を参照
	TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ARG:2}, ARG
		LOCAL:10 = RESULT
		CALLFORM SKILL_ADDTIONAL_TYPE_{ARG:2}, ARG
		LOCAL:11 = RESULT
		CALLFORM SKILL_ADDTIONAL_BASE_RATE_{ARG:2}, ARG
		LOCAL:12 = RESULT
		CALLFORM SKILL_ADDTIONAL_MAX_RATE_{ARG:2}, ARG
		LOCAL:13 = RESULT
	CATCH
	ENDCATCH
	
	FLAG:拡散フラグ = LOCAL:17
	FOR LOCAL:9,LOCAL:7,LOCAL:8
		;PRINTFORML 距離は{MEASURE(ARG:1,LOCAL:9)}、拡散は{FLAG:拡散フラグ}
		SIF FLAG:拡散フラグ != MEASURE(ARG:1,LOCAL:9)
			CONTINUE
		FOR LOCAL:6,0,LOCAL:1
			FLAG:공격횟수 = LOCAL:6+1
			LOCALS = 포지션{LOCAL:9}
			SIF FLAG:LOCALS < 0
				CONTINUE
			SIF GET_STATE(CFLAG:(FLAG:LOCALS):ステート) == "DYING"
				CONTINUE
			PRINTFORM 　　TARGET:[{CPOS(FLAG:LOCALS),2}] %CALLNAME:(FLAG:LOCALS),20,LEFT%　>>>>>>　
			CALLFORM JUDG_HIT_{LOCAL:5},ARG,FLAG:LOCALS,LOCAL:2
			IF RESULT == 1
				;명중した
				CALL ATTACK_HIT,ARG,FLAG:LOCALS,LOCAL:3,LOCAL:4,LOCAL:5,LOCAL:10,LOCAL:11,LOCAL:12,LOCAL:13,LOCAL:14,LOCAL:15
			
				SIF !ARG:3
					WAIT
				;구상用に数値確保
				;CALL BATTLE_EVENT_ATTACK, ARG, ARG:1, 0, 3
			ELSE
				;外した
				PRINTL MISS
				SIF !HAVE_SKILL(ARG, [[스킬:엑스트라원]] ) && !CFLAG:ARG:PTフラグ && BATTLE_SETTING_IS_1MORE()
					CFLAG:ARG:１moreフラグ -= 1
				SIF TFLAG:스웨이리액트 && NO:(POS(LOCAL:9)) == [[キャラ:로어=아=루아]]
					num = num + 1
					num1 = FLAG:LOCALS
				SIF !ARG:3
					WAIT
				;구상用に数値確保
				CALL BATTLE_EVENT_ATTACK, ARG, FLAG:LOCALS, 0, 2
			ENDIF
			FLAG:공격횟수 = 0
		NEXT
		CALL RECEIVE_SET,FLAG:LOCALS,LOCAL:4,LOCAL:5
	NEXT
NEXT
SIF num > 0
	CALL SPECIAL_ACTION_3537,CPOS(num1),CPOS(ARG)
FLAG:拡散フラグ = 0
;=========================================
;공격명중処理
;ARG:0 사용者
;ARG:1 대상
;ARG:2 威힘
;ARG:3 相性
;ARG:4 스킬の타입(1物理,2魔法,3총)
;ARG:5 상태이상
;ARG:6 상태이상　相性
;ARG:7 상태이상　확률
;ARG:8 상태이상　최대확률
;ARG:9 흡수率
;ARG:10クリティカル率
;=========================================
@ATTACK_HIT,ARG,ARG:1,ARG:2,ARG:3,ARG:4,ARG:5,ARG:6,ARG:7,ARG:8,ARG:9,ARG:10
;명중した
IF (MAXBASE:(ARG:1):(GET_TYPE(ARG:3)) == 999 && !CFLAG:(ARG:1):(GET_TYPE(ARG:3)+"가드킬")) || (CFLAG:(ARG:1):물리반사フラグ || (CHECK_SKILL(ARG:1,2425) && !CHECK_SKILL(ARG,2449) && RAND:5 > 3)) && ARG:3 < 4 || (CFLAG:(ARG:1):마법반사フラグ && (ARG:3 > 3 && ARG:3 != 17) || CFLAG:(ARG:1):(GET_TYPE(ARG:3)+"웨이트") == 2)
	PRINTL 아차 반사되었다
	SIF !HAVE_SKILL(ARG, [[스킬:엑스트라원]] ) && !CFLAG:ARG:PTフラグ && BATTLE_SETTING_IS_1MORE()
		CALL ONE_MORE_POINT_DOWN,ARG,2
	PRINTFORM 　　TARGET:[{CPOS(ARG),2}] %CALLNAME:ARG,20,LEFT%　>>>>>>　
	IF ARG:4 == 3
		CALL DAMAGE_GUN,ARG,ARG,ARG:2,ARG:3,ARG:10,ARG:9
	ELSE
		CALLFORM DAMAGE_{ARG:4},ARG,ARG,ARG:2,ARG:3,ARG:10,ARG:9
	ENDIF
	IF RESULT > 0 && ARG:5 > 0 && CFLAG:(ARG):ステート != GET_STATE_NUM("DYING")
		PRINT  & 
		CALL ATTACK_BADSTATE,ARG,ARG,ARG:5,ARG:6,ARG:7,ARG:8
	ENDIF
	CALL BATTLE_EVENT_ATTACK, ARG, ARG:1, 0, 3
	PRINTL
ELSEIF CFLAG:(ARG:1):무효フラグ &&  ARG:3 != 17
	PRINTFORMW 공격을 무효화!
	SIF !HAVE_SKILL(ARG, [[스킬:엑스트라원]] ) && !CFLAG:ARG:PTフラグ && BATTLE_SETTING_IS_1MORE()
		CFLAG:ARG:１moreフラグ -= 1
	
		CFLAG:(ARG:1):무효フラグ = 0
;潜在방어
ELSEIF ABL:(ARG:1):잠재능력 == 2 && TALENT:(ARG:1):페르소나구사자 >= 1
	CALL SENZAI_BOUGYO,ARG,ARG:1,ARG:4,ARG:2
	IF RESULT != 1
		IF ARG:4 == 3
			CALL DAMAGE_GUN,ARG,ARG:1,ARG:2,ARG:3,ARG:10,ARG:9
		ELSE
			CALLFORM DAMAGE_{ARG:4},ARG,ARG:1,ARG:2,ARG:3,ARG:10,ARG:9
		ENDIF
		IF RESULT > 0 && ARG:5 > 0 && CFLAG:(ARG:1):ステート < GET_STATE_NUM("DYING")
			PRINT  & 
			CALL ATTACK_BADSTATE,ARG,ARG:1,ARG:5,ARG:6,ARG:7,ARG:8
		ELSE
			PRINTL
		ENDIF
	ENDIF
	SIF BASE:(ARG:1):ＨＰ <= 0
		CALL DEAD_CHARA,ARG:1
ELSE
	IF ARG:4 == 3
		CALL DAMAGE_GUN,ARG,ARG:1,ARG:2,ARG:3,ARG:10,ARG:9
	ELSE
		CALLFORM DAMAGE_{ARG:4},ARG,ARG:1,ARG:2,ARG:3,ARG:10,ARG:9
	ENDIF
	IF RESULT > 0 && ARG:5 > 0 && ARG:1 > -1 && CFLAG:(ARG:1):ステート != GET_STATE_NUM("DYING")
		PRINT  & 
		CALL ATTACK_BADSTATE,ARG,ARG:1,ARG:5,ARG:6,ARG:7,ARG:8
	ELSE
		PRINTL
	ENDIF
	;潜在補助・회복・공격
	IF ARG:1 > -1 && BASE:(ARG:1):ＨＰ > 0  && TALENT:(ARG:1):페르소나구사자 >= 1 && (ABL:(ARG:1):잠재능력 == 6 || ABL:(ARG:1):잠재능력 == 1 || ABL:(ARG:1):잠재능력 == 5 || ABL:(ARG:1):잠재능력 == 0)
		CALL SENZAI_HO_KA_KO,ARG,ARG:1
	ENDIF
ENDIF
SIF !FLAG:カウンター中
	CFLAG:(ARG:1):회피실패 = 1

;=========================================
;拡散공격用、距離測定関数
;=========================================
@MEASURE(ARG,ARG:1)
#FUNCTION
SELECTCASE ARG
	CASE IS < 4
		SELECTCASE ARG:1
			CASE IS < 4
				RETURNF ABS(ARG-ARG:1)
			CASE IS < 7
				RETURNF ABS(ARG-((ARG:1-3)))+1
		ENDSELECT
	CASE IS < 7
		SELECTCASE ARG:1
			CASE IS < 4
				RETURNF ABS(ARG-((ARG:1+3)))+1
			CASE IS < 7
				RETURNF ABS(ARG-ARG:1)
		ENDSELECT
	CASE IS < 12
		SELECTCASE ARG:1
			CASE IS < 12
				RETURNF ABS(ARG-ARG:1)
			CASE IS < 17
				RETURNF ABS(ARG-((ARG:1-5)))+1
		ENDSELECT
	CASE IS < 17
		SELECTCASE ARG:1
			CASE IS < 12
				RETURNF ABS(ARG-((ARG:1+5)))+1
			CASE IS < 17
				RETURNF ABS(ARG-ARG:1)
		ENDSELECT
ENDSELECT
RETURNF -1

;=========================================
;1체회복스킬기본
;ARG:0 사용者
;ARG:1 대상
;ARG:2 사용스킬
;ARG:3 真の場合最後にWAITで待たない
;=========================================
@HEAL_SINGLE,ARG,ARG:1,ARG:2,ARG:3
;コストを支払う
;CALL PAY_COST,ARG,ARG:2

CALLFORM  SKILL_POWER_{ARG:2}
LOCAL:3 = RESULT
;LOCAL:3 스킬威힘

PRINTFORM 　　TARGET:[{CPOS(ARG:1),2}] %CALLNAME:(ARG:1),20,LEFT%　>>>>>>　


;회복量
LOCAL = LOCAL:3 * (MAXBASE:ARG:마법효과/4+(MAXBASE:ARG:마법위력 / 8+10)) /50
LOCAL = MAX(LOCAL,0)
;회복素質で倍加
CALL 相性소질체크, ARG, 4, 0
SIF RESULT == 1
	LOCAL = LOCAL * 2
SIF CFLAG:(ARG:1):ステート == GET_STATE_NUM("BRAND")
	LOCAL = 1
SIF CFLAG:(ARG:1):회복不能フラグ
	LOCAL = 0
PRINTFORML {LOCAL}회복
SIF !ARG:3
	WAIT
CALL VAR_HP,(ARG:1),LOCAL,3
;東方ＭＯＤ추가--------------------
;八意永琳：비약「선향옥토」　1체회복스킬사용時に상태이상회복
SIF CHECK_SKILL(ARG,2864)
	CALL SPECIAL_ACTION_2864,ARG,ARG:1
;東方ＭＯＤ추가ここまで------------


;=========================================
;範囲회복스킬기본
;ARG:0 사용者
;ARG:1 대상
;ARG:2 사용스킬
;ARG:3 真の場合最後にWAITで待たない
;=========================================
@HEAL_FIELD,ARG,ARG:1,ARG:2,ARG:3
;コストを支払う
;CALL PAY_COST,ARG,ARG:2


;회복する範囲を参照
IF ARG:1 == 17
	LOCAL:7 = 1
	LOCAL:8 = 4
ELSEIF ARG:1 == 18
	LOCAL:7 = 4
	LOCAL:8 = 7
ELSEIF ARG:1 == 19
	LOCAL:7 = 1
	LOCAL:8 = 7
ELSEIF ARG:1 == 20
	LOCAL:7 = 7
	LOCAL:8 = 12
ELSEIF ARG:1 == 21
	LOCAL:7 = 12
	LOCAL:8 = 17
ELSEIF ARG:1 == 22
	LOCAL:7 = 7
	LOCAL:8 = 17
ENDIF

CALLFORM  SKILL_POWER_{ARG:2}
LOCAL:3 = RESULT
;LOCAL:3 스킬威힘

FOR LOCAL:9,LOCAL:7,LOCAL:8
	LOCALS = 포지션{LOCAL:9}
	SIF FLAG:LOCALS < 0
		CONTINUE
	SIF GET_STATE(CFLAG:(FLAG:LOCALS):ステート) == "DYING"
		CONTINUE
	PRINTFORM 　　TARGET:[{CPOS(FLAG:LOCALS),2}] %CALLNAME:(FLAG:LOCALS),20,LEFT%　>>>>>>　
	
	;회복量
	LOCAL = LOCAL:3 * (MAXBASE:ARG:16/4+(MAXBASE:ARG:15 / 8+10)) /50
	LOCAL = MAX(LOCAL,0)
	;회복素質で倍加
	CALL 相性소질체크, ARG, 4, 0
	SIF RESULT == 1
		LOCAL = LOCAL * 2
	SIF CFLAG:(FLAG:LOCALS):ステート == GET_STATE_NUM("BRAND")
		LOCAL = 1
	SIF CFLAG:(FLAG:LOCALS):회복不能フラグ
		LOCAL = 0
	PRINTFORML {LOCAL}회복
	CALL VAR_HP,FLAG:LOCALS,LOCAL,3

NEXT
SIF !ARG:3
	WAIT

;=========================================
;HP全회복
;ARG:0 대상キャラNo.
;ARG:1 대상にPT外のキャラも含めるか? 0=含めない 1=含める
;ARG:2 最後にWAITで待たないか? 0=待つ 1=待たない
;LOCAL:0  = キャラクターのPT内位置 CPOS(ARG:0) 1〜16
;LOCALS:0 = 대상キャラのPT内位置표시　PT内にいない場合は"--"で표시
;=========================================
@ALL_HEAL, ARG:0 = -1, ARG:1 = 0, ARG:2 = 1
;이상なキャラNo.の指定
SIF (ARG:0) < 0
	RETURN 0
LOCAL:0 = CPOS(ARG:0)
;대상キャラをPT内に限定する場合PT範囲内か?
IF (ARG:1) == 0
	SIF (LOCAL:0) < 0 || (LOCAL:0) > 16
		RETURN 0
ENDIF
;대상が死んでいる場合は회복不可
SIF GET_STATE(CFLAG:(ARG:0):ステート) == "DYING"
	RETURN 0

;位置표시설정
IF 0 < (LOCAL:0) && (LOCAL:0) < 17
	LOCALS:0 = {(LOCAL:0),2}
ELSE
	LOCALS:0 = --
ENDIF

;HP회복処理
IF CFLAG:(ARG:0):회복不能フラグ > 0
	PRINTFORMW 　　TARGET:[%(LOCALS:0)%] %CALLNAME:(ARG:0),20,LEFT%　>>>>>>　0회복
ELSEIF CFLAG:(ARG:0):ステート == GET_STATE_NUM("BRAND")
	PRINTFORMW 　　TARGET:[%(LOCALS:0)%] %CALLNAME:(ARG:0),20,LEFT%　>>>>>>　1회복
	CALL VAR_HP,(ARG:0),1,3
ELSE
	PRINTFORMW 　　TARGET:[%(LOCALS:0)%] %CALLNAME:(ARG:0),20,LEFT%　>>>>>>　{MAXBASE:(ARG:0):ＨＰ - BASE:(ARG:0):ＨＰ}회복
	CALL VAR_HP, (ARG:0), MAXBASE:(ARG:0):ＨＰ, 3
ENDIF

;WAITで待つか?
SIF !(ARG:2)
	WAIT

;=========================================
;회복可能バッド스테이터스の회복
;ARG:0 대상キャラNo.
;ARG:1 대상にPT外のキャラも含めるか? 0=含めない 1=含める
;ARG:2 最後にWAITで待たないか? 0=待つ 1=待たない
;LOCAL:0  = キャラクターのPT内位置 POS(ARG:0) 1〜16
;LOCALS:0 = 대상キャラのPT内位置표시　PT内にいない場合は"--"で표시
;=========================================
@ALL_CURE_STATE, ARG:0 = -1, ARG:1 = 0, ARG:2 = 1
;이상なキャラNo.の指定
SIF (ARG:0) < 0
	RETURN 0
LOCAL:0 = CPOS(ARG:0)
;대상キャラをPT内に限定する場合PT範囲内か?
IF (ARG:1) == 0
	SIF (LOCAL:0) < 0 || (LOCAL:0) > 16
		RETURN 0
ENDIF
;대상が死んでいる場合は회복不可
SIF GET_STATE(CFLAG:(ARG:0):ステート) == "DYING"
	RETURN 0

;位置표시설정
IF 0 < (LOCAL:0) && (LOCAL:0) < 17
	LOCALS:0 = {(LOCAL:0),2}
ELSE
	LOCALS:0 = --
ENDIF

;상태이상회복処理
SELECTCASE GET_STATE(CFLAG:(ARG:0):ステート)
	CASE "FLY","GOOD","DYING","ORGY","HEAT"
		RETURN 0
	CASEELSE
		SIF GET_STATE(CFLAG:(ARG:0):ステート) == "PANIC" || GET_STATE(CFLAG:(ARG:0):ステート) == "CHARM"
			CFLAG:(ARG:0):混乱매료리카バー = 1
		PRINTFORML 　　TARGET:[%(LOCALS:0)%] %CALLNAME:(ARG:0),20,LEFT%　>>>>>>　상태회복！
		CFLAG:(ARG:0):ステート = 0
		CFLAG:(ARG:0):ステート経過ターン = 0
ENDSELECT

;WAITで待つか?
SIF !(ARG:2)
	WAIT
;直せれば1を返す
RETURN 1
;=========================================
;1체バッド스테이터스부여기본
;ARG:0 사용者
;ARG:1 대상
;ARG:2 사용스킬
;ARG:3 真の場合最後にWAITで待たない
;=========================================
@SINGLE_BADSTATE,ARG,ARG:1,ARG:2,ARG:3
;コストを支払う
;CALL PAY_COST,ARG,ARG:2

;ステートを参照
TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ARG:2},ARG
	LOCAL:6 = RESULT
	CALLFORM SKILL_ADDTIONAL_TYPE_{ARG:2},ARG
	LOCAL:7 = RESULT
	CALLFORM SKILL_ADDTIONAL_BASE_RATE_{ARG:2},ARG
	LOCAL:8 = RESULT
	CALLFORM SKILL_ADDTIONAL_MAX_RATE_{ARG:2},ARG
	LOCAL:9 = RESULT
CATCH
ENDCATCH

PRINTFORM 　　TARGET:[{CPOS(ARG:1),2}] %CALLNAME:(ARG:1),20,LEFT%　>>>>>>　
IF (MAXBASE:(ARG:1):(GET_TYPE(LOCAL:7)) == 999 && !CFLAG:(ARG:1):(GET_TYPE(LOCAL:7)+"가드킬")) || (CFLAG:(ARG:1):물리반사フラグ && LOCAL:7 < 4) || (CFLAG:(ARG:1):마법반사フラグ && (LOCAL:7 > 3 && LOCAL:7 != 17) || CFLAG:(ARG:1):(GET_TYPE(LOCAL:7)+"웨이트") == 2)
	PRINTL 아차 반사되었다
	PRINTFORM 　　TARGET:[{CPOS(ARG),2}] %CALLNAME:(ARG),20,LEFT%　>>>>>>　
	CALL ATTACK_BADSTATE,ARG,ARG,LOCAL:6,LOCAL:7,LOCAL:8,LOCAL:9
ELSEIF CFLAG:(ARG:1):무효フラグ &&  LOCAL:4 != 17
	PRINTFORMW 공격을 무효화!
	CFLAG:(ARG:1):무효フラグ = 0
ELSE
	CALL ATTACK_BADSTATE,ARG,ARG:1,LOCAL:6,LOCAL:7,LOCAL:8,LOCAL:9
ENDIF
SIF !ARG:3
	WAIT
;=========================================
;전체・列バッド스테이터스부여기본
;ARG:0 사용者
;ARG:1 대상
;ARG:2 사용스킬
;ARG:3 真の場合最後にWAITで待たない
;=========================================
@FIELD_BADSTATE,ARG,ARG:1,ARG:2,ARG:3
;コストを支払う
;CALL PAY_COST,ARG,ARG:2

;공격する範囲を参照
IF ARG:1 == 17
	LOCAL:7 = 1
	LOCAL:8 = 4
ELSEIF ARG:1 == 18
	LOCAL:7 = 4
	LOCAL:8 = 7
ELSEIF ARG:1 == 19
	LOCAL:7 = 1
	LOCAL:8 = 7
ELSEIF ARG:1 == 20
	LOCAL:7 = 7
	LOCAL:8 = 12
ELSEIF ARG:1 == 21
	LOCAL:7 = 12
	LOCAL:8 = 17
ELSEIF ARG:1 == 22
	LOCAL:7 = 7
	LOCAL:8 = 17
ENDIF


FOR LOCAL:9,LOCAL:7,LOCAL:8
	FOR LOCAL:6,0,1
		
		;추가효과を参照
		TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ARG:2}, ARG
			LOCAL:10 = RESULT
			CALLFORM SKILL_ADDTIONAL_TYPE_{ARG:2}, ARG
			LOCAL:11 = RESULT
			CALLFORM SKILL_ADDTIONAL_BASE_RATE_{ARG:2}, ARG
			LOCAL:12 = RESULT
			CALLFORM SKILL_ADDTIONAL_MAX_RATE_{ARG:2}, ARG
			LOCAL:13 = RESULT
		CATCH
		ENDCATCH
		
		LOCALS = 포지션{LOCAL:9}
		SIF FLAG:LOCALS < 0
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):ステート) == "DYING"
			CONTINUE
		PRINTFORM 　　TARGET:[{CPOS(FLAG:LOCALS),2}] %CALLNAME:(FLAG:LOCALS),20,LEFT%　>>>>>>　
		
		IF (MAXBASE:(FLAG:LOCALS):(GET_TYPE(LOCAL:11)) == 999 && !CFLAG:(FLAG:LOCALS):(GET_TYPE(LOCAL:11)+"가드킬")) || (CFLAG:(FLAG:LOCALS):물리반사フラグ && LOCAL:11 < 4) || (CFLAG:(FLAG:LOCALS):마법반사フラグ && (LOCAL:11 > 3 && LOCAL:11 != 17) || CFLAG:(FLAG:LOCALS):(GET_TYPE(LOCAL:11)+"웨이트") == 2)
			PRINTL 아차 반사되었다
			PRINTFORM 　　TARGET:[{CPOS(ARG),2}] %CALLNAME:(ARG),20,LEFT%　>>>>>>　
			CALL ATTACK_BADSTATE,ARG,ARG,LOCAL:10,LOCAL:11,LOCAL:12,LOCAL:13
			SIF !ARG:3
				WAIT
		ELSEIF CFLAG:(FLAG:LOCALS):무효フラグ &&  LOCAL:4 != 17
			PRINTFORMW 공격을 무효화!
				CFLAG:(FLAG:LOCALS):무효フラグ = 0
		ELSE
			CALL ATTACK_BADSTATE,ARG,FLAG:LOCALS,LOCAL:10,LOCAL:11,LOCAL:12,LOCAL:13
			SIF !ARG:3
				WAIT
		ENDIF
	NEXT
NEXT

;=========================================
;壁스킬기본
;ARG:0 사용者
;ARGS 効果相性
;ARG:1 대상
;ARG:2 횟수
;=========================================
@SKILL_WALL , ARG , ARGS , ARG:1 , ARG:2 , ARGS:1 = "표시"
#DIM 대상範囲 , 2
;効果を適用する範囲を参照
IF RANGE(ARG:1 , 17 , 22)
	대상範囲 = AUTO_SPLIT_INT("1_4_1_7_12_7" , "_" , ARG:1 - 17) , AUTO_SPLIT_INT("4_7_7_12_17_17" , "_" , ARG:1 - 17)
ELSE
	대상範囲 = ARG:1 , ARG:1 + 1
ENDIF

FOR LOCAL, 대상範囲:0 , 대상範囲:1
	IF POS(LOCAL) > -1
		IF CFLAG:POS(LOCAL):@"%ARGS%가드킬" >= ARG:2 * 2
			CFLAG:POS(LOCAL):@"%ARGS%가드킬" = MAX(CFLAG:POS(LOCAL):@"%ARGS%가드킬" - ARG:2 * 2 , 0 )
			SIF ARGS:1 == "표시" && !GROUPMATCH(ARG:1 , 19 , 22)
				PRINTFORMW 　　TARGET:[{POS(LOCAL),2}] %CALLNAME:POS(LOCAL),20,LEFT%　>>>>>>　%ARGS%내성무효화효과를 경감！
		ELSE
			SIF CFLAG:POS(LOCAL):@"%ARGS%무효화횟수" < ARG:2
				CFLAG:POS(LOCAL):@"%ARGS%무효화횟수" = ARG:2 - CFLAG:POS(LOCAL):@"%ARGS%가드킬" / 2
			SIF ARGS:1 == "표시" && !GROUPMATCH(ARG:1 , 19 , 22)
				PRINTFORMW 　　TARGET:[{POS(LOCAL),2}] %CALLNAME:POS(LOCAL),20,LEFT%　>>>>>>　%조사처리(ARGS, "를")% 무효화！
			CFLAG:POS(LOCAL):@"%ARGS%가드킬" = 0
		ENDIF
	ENDIF
NEXT
SIF ARGS:1 == "표시" && GROUPMATCH(ARG:1 , 19 , 22)
	PRINTFORMW TARGET:\@ ARG:1 == 19 ? PARTY # ENEMY \@ >>>>>> %조사처리(ARGS, "를")% 무효화！


;=========================================
;コスト支払い
;=========================================
@PAY_COST,ARG,ARG:1
#LOCALSIZE 6
;링케이지の支払い
IF ARG:1 >= 4000 && ARG:1 < 5000 && !CFLAG:ARG:단독링케이지
	CALLFORM 참가人数_{ARG:1}
	FOR LOCAL:1, 1, RESULT + 1
		LOCALS = 링케이지참가자{LOCAL:1}
		;コストを支払う
		CALLFORM 링케이지コスト타입_{ARG:1}, LOCAL:1
		IF RESULT == 2
			CALLFORM 링케이지コスト_{ARG:1}, LOCAL:1
			LOCAL = MAXBASE:(CFLAG:ARG:LOCALS):ＨＰ * RESULT / 100
			SIF CFLAG:(CFLAG:ARG:LOCALS):ボスフラグ
				LOCAL /= 10
			BASE:(CFLAG:ARG:LOCALS):ＨＰ -= LOCAL
		ELSE
			CALLFORM 링케이지コスト_{ARG:1}, LOCAL:1
			BASE:(CFLAG:ARG:LOCALS):ＭＰ -= RESULT
		ENDIF
	NEXT
ENDIF
;コストを支払う
CALLFORM SKILL_COSTTYPE_{ARG:1}, ARG
IF RESULT == 2
	CALLFORM SKILL_COST_{ARG:1}, ARG
	LOCAL = MAXBASE:ARG:ＨＰ * RESULT / 100
	CALL COST_CUT, LOCAL, ARG:1, ARG
	LOCAL = RESULT
	SIF CFLAG:ARG:ボスフラグ
		LOCAL /= 10
	BASE:ARG:ＨＰ -= LOCAL
ELSE
	CALLFORM SKILL_COST_{ARG:1}, ARG
	CALL COST_CUT, RESULT, ARG:1, ARG
	LOCAL = RESULT
	BASE:ARG:ＭＰ -= LOCAL
ENDIF

;=========================================
;コスト払い戻し
;=========================================
@BACK_COST,ARG,ARG:1
;コストを支払う
CALLFORM SKILL_COSTTYPE_{ARG:1}, ARG
IF RESULT == 2
	CALLFORM SKILL_COST_{ARG:1}, ARG
	LOCAL = MAXBASE:ARG:ＨＰ * RESULT / 100
	CALL COST_CUT, LOCAL, ARG:1, ARG
	LOCAL = RESULT
	SIF CFLAG:ARG:ボスフラグ
		LOCAL /= 10
	BASE:ARG:ＨＰ += LOCAL
ELSE
	CALLFORM SKILL_COST_{ARG:1}, ARG
	CALL COST_CUT, RESULT, ARG:1, ARG
	LOCAL = RESULT
	BASE:ARG:ＭＰ += LOCAL
ENDIF


;=========================================
;コスト체크
;=========================================
@CHECK_COST,ARG,ARG:1

CALLFORM SKILL_COSTTYPE_{ARG:1}, ARG
IF RESULT > 1
	IF RESULT == 2
		CALLFORM SKILL_COST_{ARG:1}, ARG
		LOCAL = RESULT
		LOCAL = MAXBASE:ARG:ＨＰ * LOCAL /100
		CALL COST_CUT, LOCAL, ARG:1, ARG
		LOCAL = RESULT
		SIF CFLAG:ARG:ボスフラグ
			LOCAL /= 10
		SIF BASE:ARG:ＨＰ <= LOCAL
			RETURN 0
	ELSE
		CALLFORM SKILL_COST_{ARG:1}, ARG
		CALL COST_CUT, RESULT, ARG:1, ARG
		LOCAL = RESULT
		SIF BASE:ARG:ＭＰ < LOCAL
			RETURN 0
	ENDIF
ENDIF
RETURN 1
@CHECK_COST_LINCAGE, ARG, ARG:1, ARG:2
CALLFORM 링케이지コスト타입_{ARG:1}, ARG:2
IF RESULT > 1
	IF RESULT == 2
		CALLFORM 링케이지コスト_{ARG:1}, ARG:2
		LOCAL = MAXBASE:ARG:ＨＰ * RESULT /100
		SIF CFLAG:ARG:ボスフラグ
			LOCAL /= 10
		SIF BASE:ARG:ＨＰ <= LOCAL
			RETURN 0
	ELSE
		CALLFORM 링케이지コスト_{ARG:1}, ARG:2
		SIF BASE:ARG:ＭＰ < RESULT
			RETURN 0
	ENDIF
ENDIF
RETURN 1

;=========================================
;コストカット
;ARG	コスト
;ARG:1	스킬番号
;ARG:2	사용者
;=========================================
@COST_CUT,ARG, ARG:1, ARG:2
#DIM L_素質番号

;コスト0の스킬(通常공격など)の場合
IF ARG == 0
	;たけし당신はコストあり
	IF (NO:(ARG:2) == 0 || NO:(ARG:2) == 4999) && (CFLAG:MASTER:당신の専攻分野 == 9)
		RETURN 1
	;通常キャラはコストなし
	ELSE
		RETURN 0
	ENDIF
ENDIF


CALLFORM SKILL_SUCCESSION_TYPE_{ARG:1}
L_素質番号 = RESULT
LOCAL = ARG

;素質コストカット、달인はコストカットできない
IF BATTLE_SETTING_IS_TALENT() && !TALENT:(ARG:2):달인
	CALL 純이능자체크, ARG:2
	IF RESULT == 1
		LOCAL = LOCAL * 7/10
	ELSEIF RESULT == 0
		CALL 相性소질체크, ARG:2, L_素質番号, 0
		SIF RESULT == 1
			LOCAL = LOCAL * 4/5
	ENDIF
ENDIF

SIF LOCAL < 1
	LOCAL = 1

RETURN LOCAL
