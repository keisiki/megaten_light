;==============================
;비디오 리스트 열람 함수
;RETURN값 - 0번: 비디오를 재생안함/비디오를 교체안함
;RETURN값 - 1번: 비디오를 재생함/비디오를 교체함
;------------------------------
;TFLAG:64 - 비디오 상영 장착변수
;TFLAG:65 - 현재 상영되고 있는 구간
;TFLAG:66 - 현재 상영되고 있는 비디오의 슬롯번호
;==============================
@VIDEO_LIST_PERUSAL
#LOCALSIZE 2
#LOCALSSIZE 2
#DIM DYNAMIC V_COUNT
REDRAW 0
LOCAL = LINECOUNT

;저장된 배열 외에는 전부 더미를 입력한다
CALL VIDEO_ARRAY_DUMMY

CUSTOMDRAWLINE =
PRINTFORML 찍은 비디오 리스트를 열람하는 코너입니다
PRINTFORML 원하는 비디오를 선택해주세요
CUSTOMDRAWLINE =

FOR V_COUNT, 0, 10
	CALL VIDEO_SELECT_DETAIL_PRINT, V_COUNT
	DRAWLINE
NEXT
SIF TFLAG:63
	SETCOLOR COLOR("aqua")
PRINTFORML [10] 저장된 기록 삭제
SIF TFLAG:63
	RESETCOLOR
SIF TFLAG:64
	PRINTFORML [11] 비디오를 정지한다
PRINTFORML [12] 열람을 끝낸다

$V_SLOT_INPUT_LOOP
INPUT
IF RESULT == 0 || RESULT == 1 || RESULT == 2 || RESULT == 3 || RESULT == 4 || RESULT == 5 || RESULT == 6 || RESULT == 7 || RESULT == 8 || RESULT == 9
	IF TFLAG:63
	;비디오 삭제모드 --> 슬롯을 선택하면 해당 비디오 기록이 삭제된다
		;선택한 슬롯의 비디오 타이틀명을 지움
		CLEARLINE LINECOUNT - LOCAL
		CSTR:TARGET:(RESULT + 100) = 
		RESTART
	ELSE
	;비디오 상영
		IF STRLENS(CSTR:TARGET:(RESULT + 100)) > 0
		;비디오 제목이 있어야지만 상영 가능
		;선택한 슬롯의 비디오를 틀어놓는다(혹은 다른 비디오를 튼다)
			REDRAW 1 ;emuera 재(再)그리기 on
			TFLAG:63 = 0 ;비디오 삭제모드 off
			TFLAG:64 = 1 ;비디오를 트는 장착 변수 ON
			TFLAG:65 = 0 ;상영구간은 맨 처음부터 시작한다!
			TFLAG:66 = RESULT ;선택한 슬롯의 비디오를 상영한다!
			VIDEO_OWNER = TARGET ;트는 비디오의 주인을 대입한다!
			RETURN 1 ;--> 비디오를 튼다 / 혹은 기존 비디오에서 딴 비디오로 바꿔 튼다 결정값
		ELSE
		;빈 슬롯을 선택할 경우, 입력란으로 돌아온다
			CLEARLINE 1
			REUSELASTLINE
			GOTO V_SLOT_INPUT_LOOP
		ENDIF		
	ENDIF
ELSEIF RESULT == 10
;저장기록 삭제모드 ON/OFF
	CLEARLINE LINECOUNT - LOCAL
	INVERTBIT TFLAG:63, 0
	RESTART
ELSEIF RESULT == 11 && TFLAG:64
;틀어져 있는 비디오를 정지한다
	TFLAG:64 = 0 ;비디오를 트는 장착 변수 OFF
	REDRAW 1 ;emuera 재(再)그리기 on
	TFLAG:63 = 0 ;비디오 삭제모드 off
	RETURN 2 ;비디오를 정지한다 결정값
ELSEIF RESULT == 12
;열람을 끝내고 나간다
	REDRAW 1 ;emuera 재(再)그리기 on
	TFLAG:63 = 0 ;비디오 삭제모드 off
	RETURN 0 ;--> 비디오를 안튼다 / 혹은 기존 비디오가 틀어지고 있는 상태를 그냥 놔둔다 결정값
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO V_SLOT_INPUT_LOOP
ENDIF

