;-------------------------------------------------
;비디오열람
;-------------------------------------------------
@CHECK_COM_ABLE36
;비디오 촬영중이면 안됨
IF TEQUIP:28
	RETURN 0
ELSE
	RETURN 1
ENDIF

@ACT_COM36
#LOCALSIZE 2
#LOCALSSIZE 2
#DIM DYNAMIC TEMPSELECTCOM ;커맨드번호 임시저장 변수

PRINTL 비디오열람
TSTR:0 = 비디오열람
;CALL KOJO_MESSAGE_COM

;비디오 열람 함수 실행
CALL VIDEO_LIST_PERUSAL
IF RESULT == 1
;비디오를 재생함/비디오를 교체함
	TEMPSELECTCOM = SELECTCOM ;현 커맨드번호 임시저장
	SELECTCOM = 29 ;옛 비디오감상 커맨드번호 대입
	IF TEQUIP:주도권
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% %CALLNAME:PLAYER%에게 보라는 듯이 자기가 찍힌 비디오를 틀었다
	ELSE
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% %조사처리(CALLNAME:TARGET,"가")% 음란하게 찍힌 비디오를 틀었다
	ENDIF
	;캐릭 대사 출력
	CALL PRINT_KOJO_OF_MOVIE_AUDIENCE
	SELECTCOM = TEMPSELECTCOM ;현 커맨드번호 복구
	
	;커맨드 실행의 소스 추가
	SOURCE:TARGET:무서움 = 1000
	SOURCE:TARGET:복종 = 1000
	SOURCE:TARGET:굴종 = 500
	SOURCE:TARGET:일탈 = 200
	SOURCE:TARGET:반감 = 200

ELSEIF RESULT == 2
;틀어져 있는 비디오를 정지함
	IF TEQUIP:주도권
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 틀어져 있는 비디오를 리모컨으로 껐다
	ELSE
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 틀어져 있는 비디오를 리모컨으로 껐다
	ENDIF
ELSE
;비디오를 재생안함/비디오를 교체안함
	IF TFLAG:64
	;비디오가 틀어져 있을 경우
		IF TEQUIP:주도권
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 틀어져 있는 비디오를 그대로 놔두었다
		ELSE
			PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 틀어져 있는 비디오를 그대로 놔두었다
		ENDIF
	ELSE
		IF TEQUIP:주도권
			PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 결국 비디오를 틀지 않았다
		ELSE
			PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 결국 비디오를 틀지 않았다
		ENDIF
	ENDIF
ENDIF



;-------------------------------------------------
;커맨드 실행 체력감소
;-------------------------------------------------
;소비체력、기력
DOWNBASE:TARGET:체력 += 0
DOWNBASE:TARGET:기력 += 0

;-------------------------------------------------
;경험の処理
;-------------------------------------------------
;의존도ベクトル
TFLAG:33 = 1

;의존도보정値
TFLAG:30 += 2

RETURN 1


;-------------------------------------------------
;비디오 실시간 시청
;TFLAG:65 - 현재 상영되고 있는 구간
;TFLAG:66 - 현재 상영되고 있는 비디오의 슬롯번호
;-------------------------------------------------
@EQUIP_COM36
#LOCALSIZE 2
#LOCALSSIZE 2
IF (CDFLAG:VIDEO_OWNER:(TFLAG:66 + 10):(TFLAG:65) >= 0) && (TFLAG:65 < 10)
;비디오가 끝나지 않았다
	PRINTL
	IF TARGET == VIDEO_OWNER
	;현재 상영중인 비디오가 현 노예의 것일 경우
		PRINTFORML ◈비디오 재생中◈
	ELSE
	;현재 상영중인 비디오가 현 노예의 것이 아닐 경우
		PRINTFORML ◈비디오 재생中 (%CALLNAME:VIDEO_OWNER%의 비디오)◈
	ENDIF
	PRINTFORML 현재 상영구간:【%TRAINNAME:(CDFLAG:VIDEO_OWNER:(TFLAG:66 + 10):(TFLAG:65))%】
	TFLAG:65 += 1 ;구간을 이동
	
	;비디오 상영의 소스 추가
	SOURCE:TARGET:무서움 = 1000
	SOURCE:TARGET:복종 = 1000
	SOURCE:TARGET:굴종 = 500
	SOURCE:TARGET:일탈 = 200
	SOURCE:TARGET:반감 = 200
ELSE
;비디오가 끝났다
	PRINTL
	PRINTFORMW 《 비디오를 다 보았습니다, 지금까지 시청해주셔서 감사합니다 》
	TFLAG:64 = 0 ;비디오를 트는 장착 변수 OFF	
ENDIF



;===================================================
;조교타입
;===================================================
@COMTYPE_36
RETURN COMTYPE("커뮤계")



;==============================
;==============================
;비디오 관람객 대화구상 실행
;==============================
;==============================
@PRINT_KOJO_OF_MOVIE_AUDIENCE
#LOCALSIZE 2
#LOCALSSIZE 2

IF CFLAG:TARGET:199 >= 50
;범용구상
	TRYCCALLFORM KOJO_MESSAGE_COM_PUB{ABL:TARGET:회화타입}
	CATCH
	ENDCATCH
ELSE
;전용구상
	IF CFLAG:TARGET:199 > 0
	;전용 별도구상
		TRYCCALLFORM KOJO_MESSAGE_COM_K{NO:TARGET}_{CFLAG:TARGET:199}
		CATCH
		ENDCATCH
	ELSE
	;통상 전용구상
		TRYCCALLFORM KOJO_MESSAGE_COM_K{NO:TARGET}
		CATCH
		ENDCATCH
	ENDIF
ENDIF