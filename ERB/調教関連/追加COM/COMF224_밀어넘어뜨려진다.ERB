;밀어넘어뜨려지는 게 가능한 조건(예전)
;((!(CFLAG:초기주도권 || TALENT:쾌감을부정 || TALENT:저항 || TALENT:억압) && TALENT:남자 ) || ABL:순종 > 2) && PLAYER == MASTER
;-------------------------------------------------
;밀어넘어뜨려진다/발버둥친다
;-------------------------------------------------
@CHECK_COM_ABLE224
#LOCALSSIZE 1
;촉수 조교중이면 안됨
SIF TEQUIP:25
	RETURN 0
;삼각목마 사용중이면 안됨
SIF TEQUIP:41
	RETURN 0
;아이마스크사용중은 안돼
SIF TEQUIP:18
	RETURN 0
;밧줄사용중은 안돼
SIF TEQUIP:19
	RETURN 0
;애널전극中이면 안돼
SIF TEQUIP:23
	RETURN 0
;유방전극中이면 안돼
SIF TEQUIP:24
	RETURN 0
;착유기사용중은 안돼
SIF TEQUIP:17
	RETURN 0
;수치플레이中이면 안돼
SIF TEQUIP:30
	RETURN 0
;밀어넘어뜨려지는 게 가능한 조건(현재)
IF (TALENT:연모 || TALENT:친애 || TALENT:복종 || TALENT:예속) && PLAYER == MASTER
	RETURN 1
ELSE
	RETURN 0
ENDIF

@COMNAME224,ARG
#LOCALSSIZE 1
IF TEQUIP:주도권
	RESULTS = 발버둥친다
ELSE
	IF TFLAG:60 == 4 || TFLAG:60 == 10
	;기승위 혹은 기승위애널 중일 경우
		RESULTS = 주도권을 넘겨준다
	ELSE
	;그 외
		RESULTS = 밀어넘어뜨려진다
	ENDIF
ENDIF

@ACT_COM224
#LOCALSSIZE 1
CALL COMNAME224
LOCALS = %RESULTS%
PRINTFORML %LOCALS%
PRINTL
;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = %LOCALS%

;조교자→조교대상
TFLAG:28 = 2

;커맨드 실행 메시지
IF TEQUIP:주도권
;발버둥친다 = 주도권을 받는다
	PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 어떻게든 일어나기 위해 이리저리 발버둥쳤다
	;조교대상의 새드끼가 높을수록 주도권 만회 성공확률이 떨어진다
	IF RAND:(ABL:새드끼 + 2) == 0
	;성공
		SETCOLOR 0x58faf4
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 주도권을 만회했다!
		RESETCOLOR
		;만약 삽입(기승위)되어 있을 경우, 정상위(혹은 정상위애널)로 이행한다
		IF TFLAG:60 > 0
			IF TFLAG:60 == 10
			;기승위애널이었을 경우
				TFLAG:60 = 11 ;정상위애널로 이행
			ELSEIF TFLAG:60 == 4
			;기승위였을 경우
				TFLAG:60 = 5 ;정상위로 이행
			ENDIF
		ENDIF
		TEQUIP:주도권 = 0 ;남자주도가 된다
		;조수가 있다면 조수 입장에서도 주도권을 뺏긴 게 된다
		IF ASSI >= 0
			TEQUIP:ASSI:주도권 = 0
			SIF TFLAG:61 > 0 ;주도권을 주인에게 뺏길시, 조수는 정사에서 빠진다
				TFLAG:61 = 0
			SIF TFLAG:62 > 0 ;주도권을 주인에게 뺏길시, 조수기승위 삽입유지는 해제된다
				TFLAG:62 = 0 ;조수기승위 유지변수 OFF
		ENDIF
	ELSE
	;실패
		SETCOLOR 0xff0000
		PRINTFORMW 허나 아쉽게도 그 시도는 실패로 돌아가고 말았고, 이로 인해 %CALLNAME:TARGET%의 공세는 더 격렬해져 갔다!
		RESETCOLOR
	ENDIF
ELSE
;밀어넘어뜨린다 = 주도권을 건네준다
	IF TFLAG:60 == 4 || TFLAG:60 == 10
	;기승위 혹은 기승위애널 중일 경우
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 자신의 위에 올라타 있는 %CALLNAME:TARGET%에게 주도권을 건네주었다!
	ELSEIF TFLAG:60 == 5 || TFLAG:60 == 11
	;정상위 혹은 정상위애널 중일 경우
		PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% 연결된 상태로 %CALLNAME:PLAYER%의 가슴을 밀어넘어뜨렸다!
		IF TFLAG:60 == 11
		;정상위애널이었을 경우
			TFLAG:60 = 10 ;기승위애널로 이행
		ELSEIF TFLAG:60 == 5
		;정상위였을 경우
			TFLAG:60 = 4 ;기승위로 이행
		ENDIF
	ELSE
	;그 외
		PRINTFORMW 요염하게 미소지으면서 %조사처리(CALLNAME:TARGET,"가")% %CALLNAME:PLAYER%에게 다가간다…
		SETCOLOR 0xfe2ef7
		PRINTFORMW %조사처리(CALLNAME:PLAYER,"는")% 밀어 넘어뜨려져 버렸다…
		RESETCOLOR
	ENDIF
	TEQUIP:주도권 = 1 ;여자주도가 된다
	;조수가 있다면 조수 입장에서도 주도권을 뺏은 게 된다
	SIF ASSI >= 0
		TEQUIP:ASSI:주도권 = 1
	;비디오 촬영중일 경우
	IF TEQUIP:28
		PRINTL
		IF FLAG:22 == 1
		;촬영자가 주인일 경우
			IF ASSI >= 0
			;조수가 존재하는 경우 → 조수가 카메라를 가져간다(조수가 촬영자가 된다)
				FLAG:22 = 2
				PRINTFORMW %조사처리(CALLNAME:PLAYER,"가")% 가지고 있던 비디오카메라를 %조사처리(CALLNAME:ASSI,"가")% 낚아채갔다
				PRINTFORMW %조사처리(CALLNAME:ASSI,"가")% 못다찍은 비디오를 계속 찍어나가기 시작했다
			ELSE
			;조수가 없는 경우 → 강제 촬영종료
				TEQUIP:28 = 0
				ITEM:43 -= 1
				CALL PRICE_VIDEO
				PRINTFORMW 그때 %조사처리(CALLNAME:PLAYER,"가")% 들고 있었던 비디오카메라가 바닥에 떨어져 굴렀다
				SETCOLOR 0xff0040
				PRINTFORMW 까강! 떼굴떼굴! 떼구르르르르~!
				RESETCOLOR
				SETCOLOR 0x848484
				PRINTFORMW 칙! 치이익! 치이이이이이이이이이이~! (노이즈 소리)
				RESETCOLOR
				PRINTW ★★★비디오 촬영을 종료합니다★★★
			ENDIF
		ELSEIF FLAG:22 == 2
		;촬영자가 조수일 경우
			PRINTFORMW 참고로 비디오는 %조사처리(CALLNAME:ASSI,"가")% 계속 찍고 있는 중이다
		ENDIF
	ENDIF
ENDIF

TFLAG:13 = 44


;-------------------------------------------------
;ソース추가処理
;-------------------------------------------------
;소비체력、기력
DOWNBASE:TARGET:체력 += 10
DOWNBASE:TARGET:기력 += 10

SOURCE:TARGET:달성 = 10
SOURCE:TARGET:노출 = 50
SOURCE:TARGET:굴종 = 80
SOURCE:TARGET:일탈 = 30
;연모나 친애가 아닐 경우, 무서움 및 반감이 증가
IF !(TALENT:3 || TALENT:6)
	SOURCE:TARGET:반감 = 150
	SOURCE:TARGET:무서움 = 120
ENDIF
;비디오 찍을때
IF TEQUIP:28
	SOURCE:TARGET:액체 += 50
	SOURCE:TARGET:욕정 += 100
	;연모나 친애가 아닐 경우, 반감이 증가
	SIF !(TALENT:3 || TALENT:6)
		SOURCE:TARGET:반감 += 200
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;억압、저항持ちには、억울ソース추가
SIF TALENT:32 || TALENT:34
	SOURCE:TARGET:울굴 += 150

;처녀、정조관념
IF TALENT:0
	TIMES SOURCE:TARGET:무서움 , 1.20
	TIMES SOURCE:TARGET:굴종 , 1.20
	;연모나 친애가 아닐 경우, 반감이 증가
	SIF !(TALENT:3 || TALENT:6)
		TIMES SOURCE:TARGET:반감 , 1.20
	SOURCE:TARGET:노출 += 300
	;정조관념일 경우, 노출 일탈이 증가
	IF TALENT:30
		TIMES SOURCE:TARGET:노출 , 2.00
		TIMES SOURCE:TARGET:일탈 , 2.00
	ENDIF
ENDIF

;-------------------------------------------------
;경험の処理
;-------------------------------------------------
;레즈경험

A = 1
IF IS_LESBIAN(TARGET,PLAYER)
	CALL ADDS_EXP("레즈경험", A, TARGET)
	CALL ADDS_EXP("레즈경험", A, PLAYER)
ELSEIF IS_GAY(TARGET,PLAYER)
	CALL ADDS_EXP("ＢＬ경험", A, TARGET)
	CALL ADDS_EXP("ＢＬ경험", A, PLAYER)
ENDIF

;의존도ベクトル
TFLAG:33 = 3

;의존도보정値(복종で1、예속で2）
IF TALENT:5 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:8 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1

@MESSAGE_COM224
#LOCALSSIZE 1


;===================================================
;조교타입
;===================================================
@COMTYPE_224
RETURN COMTYPE("특수커맨드계")

