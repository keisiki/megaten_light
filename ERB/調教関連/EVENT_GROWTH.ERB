;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:EVENT_GROWTH.ERB
;	Facility	:EVENT_GROWTH、음모 및 음핵의 성장과 관련 처리 - YM에서 이식
;
;	Licence		:ライセンスフリー
;
;	Modification Data:
;
;	Edit	Date				Author				Reason
;	001	2018/07/08	ㅇㅇ					新規作成
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;=============================================================================
;음모 성장 관련
;=============================================================================
;陰毛の成長具合(BASE:음모)の増減処理や、상태판정をここで行う.細かくやると面倒なため、処理は大雑把に
;剃毛プ레이等を実行(つまり초기상태のときではない場合)または脱毛상태でないときは毛が生えてくるように
;大まかな눈安として、0～50：ツルツル、51～250：生えかけ、251～1000：生えそろった(剃毛가능)……となっている
;--------------------------------------------------
;時間経過における陰毛成長処理
;--------------------------------------------------
@CHECK_DAY_PUBIC_HAIR
LOCAL = TARGET
LOCAL:1 = TFLAG:799
TARGET = TFLAG:799

;일광욕、월광욕、흡혈귀
IF TIME == 0 || TIME == 1
    LOCAL:5 = 5
    SIF TALENT:일광욕
        TIMES LOCAL:5 , 1.20
ELSE
    LOCAL:5 = 10
    SIF TALENT:월광욕
        TIMES LOCAL:5 , 1.20
ENDIF

;회복빠름、회복느림、흡혈귀、봉래인
IF TALENT:회복빠름
    TIMES LOCAL:5 , 1.30
ELSEIF TALENT:회복느림
    TIMES LOCAL:5 , 0.70
ELSEIF TALENT:봉래인
    TIMES LOCAL:5 , 1.50
ENDIF

;작은체형
SIF TALENT:유아체형
    LOCAL:5 /= 2

BASE:음모 = MIN(BASE:음모 + MAX(LOCAL:5, 1), 1000)
IF TALENT:음모없음 && BASE:음모 > 250
    PRINTFORML %CALLNAME:TARGET%의 밑쪽 털이 자라났습니다.
    PRINTFORMW %조사처리(CALLNAME:TARGET,"는")% [%TALENTNAME:129%]에서 탈출했다.
    TALENT:음모없음 = 0
ENDIF

TARGET = LOCAL
TFLAG:799 = LOCAL:1

;=============================================================================
;음핵 성장 관련
;=============================================================================
;陰核の成長具合(BASE:음핵)の増減処理や、상태판정をここで行う
;大まかな눈安として、0：成長しない、1～50：ノーマル、51～100：小指サイズ、
;101～200：男性器サイズ(150이상になると挿入가능な[肥大陰核]が付く)、201～250(최대値)：異様なサイズ(初めての時は이상경험が付く)
;……となっている
;--------------------------------------------------
;時間経過における陰核成長処理
;--------------------------------------------------
@CHECK_DAY_CLITORIS
LOCAL = TARGET
LOCAL:1 = TFLAG:799
TARGET = TFLAG:799

BASE:음핵 += (TIME == 1) ? RAND:10 # RAND:5

CALL CHECK_CLITORIS_GROWTH

TARGET = LOCAL
TFLAG:799 = LOCAL:1

;--------------------------------------------------
;陰核成長판정処理
;--------------------------------------------------
@CHECK_CLITORIS_GROWTH
SIF BASE:음핵 >= 150 && TALENT:비대음핵 == 0
    CALL HUGE_CLITORIS, TARGET
IF BASE:음핵 > 200 && (CFLAG:음핵성장 & 4) == 0 
	IF !HAVE_PENIS(TARGET)
		CALL GIGATORIS, TARGET
	ELSE
		CALL GIGADICK, TARGET
	ENDIF
ENDIF
BASE:음핵 = MIN(BASE:음핵, 250)

;--------------------------------------------------
;陰核の成長具合150이상による[肥大陰核]付加処理
;--------------------------------------------------
@HUGE_CLITORIS, ARG
PRINTFORMW %CALLNAME:ARG%의 음핵은 충분한 수준의 크기까지 성장했다.

;비대음핵 취득에 따른 대사 호출
;CALL SELF_KOJO, ARG, 125

TALENT:ARG:비대음핵 = 1
IF (CFLAG:ARG:음핵성장 & 2) == 0
    CFLAG:ARG:음핵성장 |= 2
    ;이상경험
    CALL ADDS_ABNORMAL_EXP("비대음핵", ARG)
ENDIF

;--------------------------------------------------
;陰核の成長具合200이상による이상경험付加処理
;--------------------------------------------------
@GIGATORIS, ARG
PRINTFORMW %CALLNAME:ARG%의 음핵은 비정상적인 수준의 크기까지 성장했다.

;음핵 비대화 완료에 따른 대사 호출
;CALL SELF_KOJO, ARG, 126

CFLAG:ARG:음핵성장 |= 4
;이상경험
CALL ADDS_ABNORMAL_EXP("거대음핵", ARG)

;--------------------------------------------------
;거대 자지 취득
;--------------------------------------------------
@GIGADICK, ARG
PRINTFORMW %CALLNAME:ARG%의 물건은 비정상적인 수준의 크기까지 성장했다.

;음핵 비대화 완료에 따른 대사 호출
;CALL SELF_KOJO, ARG, 126

TALENT:ARG:크고아름다움 = 1
