


@LVUP_AUTOIF2_DEFAULT(ARG)

SIF EXISTCSV(NO:ARG) == 0
	GOTO SET_BASE
;元비전투원暫定処理
SIF CSVTALENT(NO:ARG,GETNUM(TALENT,"비전투원"),0)
	GOTO SET_BASE
;랜덤キャラ暫定対応
SIF INRANGE(NO:ARG, 4901, 4912)
	GOTO SET_BASE

LOCAL:1 = CSVBASE(NO:ARG,GETNUM(BASE,"힘")) + CSVBASE(NO:ARG,GETNUM(BASE,"지혜")) + CSVBASE(NO:ARG,GETNUM(BASE,"마력")) + CSVBASE(NO:ARG,GETNUM(BASE,"인내력")) + CSVBASE(NO:ARG,GETNUM(BASE,"속도")) + CSVBASE(NO:ARG,GETNUM(BASE,"운"))
SIF LOCAL:1 == 0
	GOTO SET_BASE
FOR LOCAL,0,6
	CFLAG:ARG:(GETNUM(CFLAG,"힘上昇率")+LOCAL) = CSVBASE(NO:ARG,GETNUM(BASE,"힘")+LOCAL) * 100 / LOCAL:1
NEXT
;全てが同じ平坦타입の場合は現在스테이터스から反映
IF GROUPMATCH(CFLAG:ARG:힘上昇率,CFLAG:ARG:知上昇率,CFLAG:ARG:魔上昇率,CFLAG:ARG:耐上昇率,CFLAG:ARG:速上昇率,CFLAG:ARG:운上昇率) == 5
	$SET_BASE
	LOCAL:1 = MAX(SUMARRAY(BASE:ARG:0,GETNUM(BASE,"힘"),GETNUM(BASE,"운")+1), 1)
	FOR LOCAL,0,6
		CFLAG:ARG:(GETNUM(CFLAG,"힘上昇率")+LOCAL) = BASE:ARG:(GETNUM(BASE,"힘")+LOCAL) * 100 / LOCAL:1
	NEXT
ENDIF

@LISTVIEW_LVUP_AUTOIF2(ARG)
IF FLAG:カスタムゲーム화면
	PRINTFORM [{ARG,2}] %CALLNAME:ARG,24,LEFT%
ELSE
	PRINTFORM [{ARG,2}] %CALLNAME:ARG,16,LEFT%
ENDIF
LOCAL:2 = SUMARRAY(CFLAG:ARG:0,GETNUM(CFLAG,"힘上昇率"),GETNUM(CFLAG,"운上昇率")+1)
IF LOCAL:2 == 0
	CALL LVUP_AUTOIF2_DEFAULT(ARG)
	LOCAL:2 = SUMARRAY(CFLAG:ARG:0,GETNUM(CFLAG,"힘上昇率"),GETNUM(CFLAG,"운上昇率")+1)
ENDIF
		IF LOCAL:2 == 0
			LOCAL:2 = 1
		ENDIF
FOR LOCAL:1,0,6
	LOCAL:3 = CFLAG:ARG:(GETNUM(CFLAG,"힘上昇率")+LOCAL:1)
	LOCAL:4 = LOCAL:3 * 1000 / LOCAL:2
	LOCALS = {LOCAL:3,3}\({LOCAL:4/10,2}.{LOCAL:4%10}\%\)
	CALL SETCOLOR_AUTOIF2(LOCAL:4)
	PRINTFORM %BASENAME:(GETNUM(BASE,"힘")+LOCAL:1),4,LEFT%%LOCALS,8%　　
NEXT
RESETCOLOR
PRINTL

@CASTING_LVUP_AUTOIF2(ARG)
SIF ARG < 0 || ARG >= CHARANUM
	RETURN 0
SIF TALENT:ARG:비전투원
	RETURN 0
SIF CFLAG:ARG:この場に居ないフラグ == 1
	RETURN 0
SIF CFLAG:ARG:필터링フラグ
	RETURN 0
RETURN 1

@LVUP_AUTOIF2_SETUP
#DIM L_LINE
L_LINE = LINECOUNT

CALL INPUT_CHARA_LIST("설정할 캐릭터를 선택해주세요", "CASTING_LVUP_AUTOIF2")

LOCAL = RESULT

SIF LOCAL == 1000
	RETURN
CALL CASTING_LVUP_AUTOIF2(LOCAL)
IF RESULT
	CALL LVUP_AUTOIF2_CHARASET(LOCAL)
ENDIF	

CLEARLINE LINECOUNT-L_LINE

RESTART

;能力比率が高い程色が濃くなる関数
@SETCOLOR_AUTOIF2(ARG)
IF ARG <= 100
	LOCAL = ARG * 3 / 2 + 5
	SETCOLOR 155,LOCAL,LOCAL
ELSEIF ARG <= 300
	LOCAL = 155+(ARG-100)/2
	SETCOLOR 155,155,LOCAL
ELSE
	LOCAL = 155+(ARG-300)/4
	SETCOLOR MIN(255,LOCAL),150,255
ENDIF

@LVUP_AUTOIF2_CHARASET(ARG)
#DIM L_LINE
L_LINE = LINECOUNT

DRAWLINE
PRINTFORML %CALLNAME:ARG%의 능력상승률을 변경합니다
PRINTL 상승률을 설정해 주세요
DRAWLINE
LOCAL:2 = SUMARRAY(CFLAG:ARG:0,GETNUM(CFLAG,"힘上昇率"),GETNUM(CFLAG,"운上昇率")+1)
		IF LOCAL:2 == 0
			LOCAL:2 = 1
		ENDIF
FOR LOCAL:1,0,6
	LOCAL:3 = CFLAG:ARG:(GETNUM(CFLAG,"힘上昇率")+LOCAL:1)
	LOCAL:4 = LOCAL:3 * 1000 / LOCAL:2
	CALL SETCOLOR_AUTOIF2(LOCAL:4)
	PRINTFORML [{LOCAL:1,2}] %CFLAGNAME:(GETNUM(CFLAG,"힘上昇率")+LOCAL:1)%{LOCAL:3} \({LOCAL:4/10}.{LOCAL:4%10}\%\)
NEXT
RESETCOLOR
PRINTFORML [ 7] 프리셋을 사용한다
PRINTFORML [ 8] 기본값으로 되돌린다
PRINTFORML [ 9] 결정
ONEINPUT
LOCAL = RESULT
SELECTCASE RESULT
CASE 7
	FOR LOCAL,0,10
		CALL LVUP_AUTOIF2_PLIST(LOCAL)
		SIF !RESULT
			CONTINUE
		PRINTFORM [{LOCAL,2}] %RESULTS,16,LEFT%
		FOR LOCAL:1,0,6
			LOCAL:3 = RESULT:(LOCAL:1+1)
			LOCAL:4 = LOCAL:3 * 1000 / RESULT:7
			LOCALS = {LOCAL:3,3}\({LOCAL:4/10,2}.{LOCAL:4%10}\%\)
			CALL SETCOLOR_AUTOIF2(LOCAL:4)
			PRINTFORM %BASENAME:(GETNUM(BASE,"힘")+LOCAL:1),4,LEFT%%LOCALS,8%　　
		NEXT
		RESETCOLOR
		PRINTL
	NEXT
	PRINTL [99] 취소
	INPUT
	LOCAL = RESULT
	CALL LVUP_AUTOIF2_PLIST(LOCAL)
	IF RESULT
		FOR LOCAL,0,6
			CFLAG:ARG:(GETNUM(CFLAG,"힘上昇率")+LOCAL) = RESULT:(LOCAL+1)
		NEXT
	ENDIF
CASE 0,1,2,3,4,5,6
	CALL INPUT_MANY(0,99)
	SIF RESULT 
		CFLAG:ARG:(GETNUM(CFLAG,"힘上昇率")+LOCAL) = RESULT
	
CASE 8
	CALL LVUP_AUTOIF2_DEFAULT(ARG)
CASE 9
	RETURN
ENDSELECT
CLEARLINE LINECOUNT-L_LINE
RESTART

@LVUP_AUTOIF2_PLIST(ARG)
SELECTCASE ARG
CASE 0
	RESULTS = 物理전투
	RESULT:1 = 25
	RESULT:2 = 10
	RESULT:3 = 5
	RESULT:4 = 25
	RESULT:5 = 20
	RESULT:6 = 15
CASE 1
	RESULTS = 魔法전투
	RESULT:1 = 5
	RESULT:2 = 30
	RESULT:3 = 25
	RESULT:4 = 20
	RESULT:5 = 15
	RESULT:6 = 5
CASE 2
	RESULTS = ガンナー
	RESULT:1 = 5
	RESULT:2 = 30
	RESULT:3 = 5
	RESULT:4 = 15
	RESULT:5 = 25
	RESULT:6 = 20
CASE 3
	RESULTS = 악마변신
	RESULT:1 = 4
	RESULT:2 = 12
	RESULT:3 = 4
	RESULT:4 = 4
	RESULT:5 = 4
	RESULT:6 = 72
CASE 4
	RESULTS = 平坦
	RESULT:1 = 10
	RESULT:2 = 10
	RESULT:3 = 10
	RESULT:4 = 10
	RESULT:5 = 10
	RESULT:6 = 10
CASEELSE
	RETURN 0
ENDSELECT
RESULT:7 = SUMARRAY(RESULT,1,7)
RETURN 1
