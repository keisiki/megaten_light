@CREATE_RANDOM_NAME_IF(生成法リスト, 親:0=-1, 親:1=-1, 親:2=-1)
#DIMS DYNAMIC NAMES,17
#DIMS DYNAMIC 最有힘候補,1
#DIMS DYNAMIC 生成法,1
#DIMS DYNAMIC 生成法リスト,1
#DIM DYNAMIC 親, 3
#DIMS DYNAMIC 親の苗字, 3
#DIM DYNAMIC 苗字選択, 1
#DIM DYNAMIC CNT, 1
#DIM DYNAMIC 育て, 1
#DIM DYNAMIC 母, 1
#DIM DYNAMIC 父, 1
#DIM LCOUNT
SIF 生成法リスト == ""
	生成法リスト = 英語圏の女性_英語圏の男性_ドイツ人女性_ドイツ人男性_플랑ス人女性_플랑ス人男性_イタリア人女性_イタリア人男性_スペイン人女性_スペイン人男性_スウェーデン人女性_スウェーデン人男性_フィンランド人女性_フィンランド人男性_ロシア人女性_ロシア人男性_アラブ人女性_アラブ人男性_インド人女性_インド人男性_ギリシャ人女性_ギリシャ人男性_中国人女性_中国人男性_日本人男性_日本人女性_姫君風／中世日本_姫君風／近代日本_姫君風／中国伝奇_랜덤女性名
育て = 0
母 = 1
父 = 2
FOR CNT, 0, 3
	IF 親:CNT >= 0
		;育ての親や父親が母と同一なら표시しない
		IF CNT == 母 || 親:CNT != 親:母
			IF STRCOUNT( NAME:(親:CNT), " " ) == 1
				親の苗字:CNT = %AUTO_SPLIT( NAME:(親:CNT), " ", 0 ) + " "%
			ELSEIF STRCOUNT( NAME:(親:CNT), "・" ) == 1
				親の苗字:CNT = ・%AUTO_SPLIT( NAME:(親:CNT), "・", 1 )%
			ELSE
			;苗字を持たない親は見なかったことにする
				親:CNT = -1 
			ENDIF
		ELSE
			親:CNT = -1
		ENDIF
	ENDIF
NEXT
苗字選択 = -1
最有힘候補 = 
$INPUT_LOOP
FOR LCOUNT,0,100
	生成法 = %AUTO_SPLIT(生成法リスト,"_",LCOUNT)%
	SIF STRLENS(生成法) == 0
		BREAK
	TRYCCALLFORM NAMEDIC_%生成法%,0
		PRINTFORMLC [{LCOUNT,2}]%生成法%
	CATCH
	ENDCATCH
NEXT
PRINTL 
PRINTL 
PRINTL [100]스스로 이름을 입력한다
IF 親:育て >= 0
	IF 苗字選択 == 育て
		SETCOLOR COLOR( "P-YELLOW" )
	ENDIF
	PRINTL [101]키워준 부모의 성을 붙인다
	RESETCOLOR
ENDIF
IF 親:母 >= 0
	IF 苗字選択 == 母
		SETCOLOR COLOR( "P-YELLOW" )
	ENDIF
	PRINTL [102]모친의 성을 붙인다
	RESETCOLOR
ENDIF
IF 親:父 >= 0
	IF 苗字選択 == 父
		SETCOLOR COLOR( "P-YELLOW" )
	ENDIF
	PRINTL [103]부친의 성을 붙인다
	RESETCOLOR
ENDIF
DO
	INPUT
	SELECTCASE RESULT
		CASE 100
			PRINTL 이름을 입력해주세요
			INPUTS
			最有힘候補 = %RESULTS%
			GOTO 확인
		CASE 101 TO 103
			IF 親:( RESULT - 101 ) >= 0
				IF 苗字選択 == RESULT - 101
					苗字選択 = -1
				ELSE
					苗字選択 = RESULT - 101
				ENDIF
			ENDIF
			GOTO INPUT_LOOP
		CASE 0 TO (STRCOUNT(生成法リスト,"_") + 1)
			SWAP RESULT,LCOUNT
			VARSET RESULTS
			生成法 = %AUTO_SPLIT(生成法リスト,"_",LCOUNT)%
			IF GROUPMATCH( 生成法, "日本人女性", "日本人男性" ) && 苗字選択 >= 0
				TRYCCALLFORM NAMEDIC_%生成法%, 16, 親の苗字:苗字選択
				CATCH
					CONTINUE
				ENDCATCH
			ELSE
				TRYCCALLFORM NAMEDIC_%生成法%,16
				CATCH
					CONTINUE
				ENDCATCH
			ENDIF
			GOTO 選択
	ENDSELECT
LOOP 1

$확인
PRINTFORML 「%最有힘候補%」%조사만처리(最有힘候補,"로")% 좋습니까?
CALL INPUT_YN,"Yes","No"
SIF RESULT == 1
	GOTO INPUT_LOOP
RESULTS = %最有힘候補%
RETURN

$選択
FOR LCOUNT,1,17
	IF 苗字選択 >= 0
		IF STRCOUNT( 親の苗字:苗字選択, " " ) == 1
			NAMES:LCOUNT = %親の苗字:苗字選択%%RESULTS:(LCOUNT-1)%
		ELSEIF STRCOUNT( 親の苗字:苗字選択, "・" ) == 1
			NAMES:LCOUNT = %RESULTS:(LCOUNT-1)%%親の苗字:苗字選択%
		ELSE
		;選択が出来ているなら苗字を持っているはずだが、万一のときは苗字抜きで
			NAMES:LCOUNT = %RESULTS:(LCOUNT-1)%
		ENDIF
	ELSE
		NAMES:LCOUNT = %RESULTS:(LCOUNT-1)%
	ENDIF
	PRINTFORMLC [{LCOUNT,2}]%NAMES:LCOUNT%
NEXT
PRINTL 
PRINTL 
PRINTLC	[100]다른 후보
PRINTLC	[200]다시 생각한다
DO
	INPUT
	SELECTCASE RESULT
		CASE 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16
			最有힘候補 = %NAMES:RESULT%
			GOTO 확인
		CASE 100
			VARSET RESULTS
			CALLFORM NAMEDIC_%生成法%,16
			GOTO 選択
		CASE 200
			GOTO INPUT_LOOP
	ENDSELECT
LOOP 1
